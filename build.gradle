plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group 'com.messenger'
version '1.0.0'

repositories {
    mavenCentral()
}

javafx {
    version = '17'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {
    implementation 'org.openjfx:javafx-controls:17.0.6'
    implementation 'org.openjfx:javafx-fxml:17.0.6'

    // Log4j 2
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

application {
    mainClass = 'com.messenger.Main'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.messenger.Main',
                'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}

javadoc {
    group = 'documentation'
    description = 'Generates Javadoc API documentation'

    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    destinationDir = layout.buildDirectory.dir("docs/javadoc").get().asFile

    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        author = true
        version = true
        links = ['https://docs.oracle.com/en/java/javase/17/docs/api/']
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        addBooleanOption('html5', true)
    }
}